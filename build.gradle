//file:noinspection GrUnresolvedAccess
//file:noinspection GroovyAssignabilityCheck
import org.gradle.api.tasks.testing.logging.TestLogEvent

buildscript {
    repositories {
        google()
        mavenCentral()
        gradlePluginPortal()
    }
}

plugins {
    id 'java'
    id 'checkstyle'
    id 'maven-publish'
    id 'java-library'
    id "jacoco"
    id "com.github.johnrengelman.shadow" version "7.1.0"
    id 'com.diffplug.spotless' version '5.0.0'
    id "com.github.spotbugs" version "4.7.1"
    id "net.ltgt.errorprone" version "2.0.1"
    id 'net.ltgt.nullaway' version "1.1.0"
    id "com.dorongold.task-tree" version "1.5"
}

group 'com.nosto'
version '0.0.5'

repositories {
    mavenCentral()
    maven {
        url 'https://maven.onehippo.com/maven2/'
    }
}

dependencies {
    testRuntimeOnly group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: '5.2.0'
    testRuntimeOnly group: 'org.junit.vintage', name: 'junit-vintage-engine', version: '5.2.0'
    implementation group: 'junit', name: 'junit', version: '4.12'
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: '2.11.0'
    implementation group: 'javax.annotation', name: 'jsr305', version: '1.0'
    implementation group: 'org.apache.commons', name: 'commons-lang3', version: '3.12.0'
    implementation group: 'org.apache.logging.log4j', name: 'log4j-api', version: '2.14.1'
    implementation group: 'org.reflections', name: 'reflections', version: '0.9.12'
    implementation group: 'org.jeasy', name: 'easy-random-core', version: '4.0.0'
    implementation group: 'net.sf.oval', name: 'oval', version: '3.1.1'
    implementation group: 'com.google.guava', name: 'guava', version: '30.1-jre'
    implementation group: 'ch.digitalfondue.vatchecker', name: 'vatchecker', version: '1.5.0'
    implementation group: 'com.squareup.okhttp3', name: 'okhttp', version: '4.9.1'
    implementation group: 'commons-validator', name: 'commons-validator', version: '1.7'
    implementation group: 'org.mongodb', name: 'mongo-java-driver', version: '3.12.8'
    implementation group: 'com.googlecode.libphonenumber', name: 'libphonenumber', version: '8.12.22'
    implementation group: 'org.apache.servicemix.bundles', name: 'org.apache.servicemix.bundles.javax-inject', version: '1_3'
    testImplementation group: 'org.hamcrest', name: 'hamcrest-all', version: '1.3'
    testImplementation group: 'org.assertj', name: 'assertj-core', version: '3.19.0'
    errorprone group: 'com.uber.nullaway', name: 'nullaway', version: '0.7.9'
    errorprone group: 'com.google.errorprone', name: 'error_prone_core', version: '2.3.4'
    spotbugsPlugins group: 'com.h3xstream.findsecbugs', name: 'findsecbugs-plugin', version: '1.10.1'
}

// Configuration for the nullaway extension. The rest of the parameters must be
// specified on the java-compile tasks. The only option that be specified here
// is the name of the root package to be analysed.
nullaway {
    //noinspection GrUnresolvedAccess
    annotatedPackages.add("com.nosto")
}

// Configuration for the Jacoco plugin. It seems that it isn't possible to define
// the output report formats here and those seem to be defined on the individual
// task level.
// I'm not sure why the tool version is defined here and it isn't managed as a
// dependency.
jacoco {
    toolVersion = "0.8.5"
}

// We only need the XML report so that the CI tools can parse it. The HTML isn't needed.
jacocoTestReport {
    reports {
        csv.enabled = true
        xml.enabled = false
        html.enabled = true
    }
}

test {
    useJUnitPlatform()
    testLogging.events.addAll([TestLogEvent.PASSED, TestLogEvent.SKIPPED, TestLogEvent.FAILED])
    reports {
        junitXml.enabled = true
        html.enabled = true
    }
    maxParallelForks = (int) (Runtime.runtime.availableProcessors() / 2 + 1)
    finalizedBy jacocoTestReport // report is always generated after tests run
}

jacocoTestReport {
    dependsOn test // tests are required to run before generating the report
}

javadoc {
    options.addBooleanOption('html5', true)
}

task sourcesJar(type: Jar, dependsOn: classes) {
    archiveClassifier.set("sources")
    from sourceSets.main.allSource
}

artifacts {
    archives sourcesJar
}

// Configuration for the Spotbugs plugin. It seems that it isn't possible to define
// the output report formats here and those seem to be defined on the individual
// task level.
// I'm not sure why the tool version is defined here and it isn't managed as a
// dependency.
spotbugs {
    //noinspection GroovyAccessibility,GrFinalVariableAccess,GroovyAssignabilityCheck
    toolVersion = '4.0.2'
    //noinspection GroovyAccessibility,GrFinalVariableAccess,GroovyAssignabilityCheck
    ignoreFailures = true
    //noinspection GroovyAccessibility,GrFinalVariableAccess,GroovyAssignabilityCheck
    effort = 'max'
    // Exclude all *.scala files as Spotbugs doesn't work too well with Scala.
    //noinspection GroovyAccessibility,GrFinalVariableAccess,GroovyAssignabilityCheck
    excludeFilter = file("$rootDir/gradle/config/spotbugs/exclude.xml")
}

// We only need the XML report so that the CI tools can parse it. The HTML isn't needed.
spotbugsMain {
    //noinspection GroovyAssignabilityCheck
    reports {
        xml.enabled = true
    }
}

// We only need the XML report so that the CI tools can parse it. The HTML isn't needed.
spotbugsTest {
    //noinspection GroovyAssignabilityCheck
    reports {
        xml.enabled = true
    }
}

// Configuration for the Checkstyle plugin. It seems that it isn't possible to define
// the output report formats here and those seem to be defined on the individual
// task level.
// I'm not sure why the tool version is defined here and it isn't managed as a
// dependency.
checkstyle {
    toolVersion "7.6.1"
    config rootProject.resources.text.fromFile("gradle/config/checkstyle/checkstyle.xml")
}

// We only need the XML report so that the CI tools can parse it. The HTML isn't needed
// and must be explicitly disabled
checkstyleMain {
    reports {
        xml.enabled = true
        html.enabled = false
    }
}

// We only need the XML report so that the CI tools can parse it. The HTML isn't needed.
checkstyleTest {
    reports {
        xml.enabled = true
        html.enabled = false
    }
}

publishing {
    repositories {
        maven {
            name = "github"
            url = uri("https://maven.pkg.github.com/nosto/oval-extras")
            credentials(PasswordCredentials)
        }
    }
    publications {
        //noinspection GroovyAssignabilityCheck
        mavenJava(MavenPublication) {
            //noinspection GroovyAssignabilityCheck
            from components.java

            pom {
                //noinspection GroovyAssignabilityCheck
                name = 'Oval Extras'
                //noinspection GroovyAssignabilityCheck
                description = 'A simple library to sanity-check your bean ser-deser'
                //noinspection GroovyAssignabilityCheck
                url = 'https://github.com/nosto/oval-extras'
                licenses {
                    license {
                        //noinspection GroovyAssignabilityCheck
                        name = 'The Apache License, Version 2.0'
                        //noinspection GroovyAssignabilityCheck
                        url = 'https://www.apache.org/licenses/LICENSE-2.0.txt'
                        //noinspection GroovyAssignabilityCheck
                        distribution = 'repo'
                    }
                }
                developers {
                    developer {
                        //noinspection GroovyAssignabilityCheck
                        id = 'olsi-qose'
                        //noinspection GroovyAssignabilityCheck
                        name = 'Olsi Qose'
                        //noinspection GroovyAssignabilityCheck
                        email = 'olsi.qose@nosto.com'
                    }
                }
                contributors {
                    developer {
                        //noinspection GroovyAssignabilityCheck
                        id = 'gary-nosto'
                        //noinspection GroovyAssignabilityCheck
                        name = 'Gary Madden'
                        //noinspection GroovyAssignabilityCheck
                        email = 'gary.madden@nosto.com'
                    }
                    contributor {
                        //noinspection GroovyAssignabilityCheck
                        id = 'mridang'
                        //noinspection GroovyAssignabilityCheck
                        name = 'Mridang Agarwalla'
                        //noinspection GroovyAssignabilityCheck
                        email = 'mridang.agarwalla@osto.com'
                    }
                }
                scm {
                    //noinspection GroovyAssignabilityCheck
                    connection = 'scm:git:git://github.com/nosto/oval-extras.git'
                    //noinspection GroovyAssignabilityCheck
                    developerConnection = 'scm:git:ssh://github.com/nosto/oval-extras.git'
                    //noinspection GroovyAssignabilityCheck
                    url = 'https://github.com/nosto/oval-extras'
                }
                issueManagement {
                    //noinspection GroovyAssignabilityCheck
                    system = "Github"
                    //noinspection GroovyAssignabilityCheck
                    url = "https://github.com/nosto/oval-extras/issues"
                }
            }
        }
    }
}

// Task for building the zip file for upload
task buildZip(type: Zip) {
    zip64 = true
    archiveBaseName.set(rootProject.name)
    from compileJava
    from processResources
    into('lib') {
        from configurations.runtimeClasspath
    }
}

shadowJar {
    zip64 true
}

build {
    dependsOn buildZip
}
